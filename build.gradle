buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mc_version}-${mod_version}"
group= "mrriegel.storagenetwork"
archivesBaseName = "SimpleStorageNetwork"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"
    mappings = "stable_39"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    useDepAts = true
    replace '@FINGERPRINT@', project.findProperty('signSHA1')
	replaceIn "StorageNetwork.java"
    replace "@VERSION@", "${mod_version}"
}

task signJar(type: SignJar, dependsOn: reobfJar) {

     // Skips if the keyStore property is missing.
    onlyIf {
        project.hasProperty('keyStore')
    }

     // findProperty allows us to reference the property without it existing.
    // Using project.propName would cause the script to fail validation if
    // the property did not exist.
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

dependencies {
   deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"
   deobfCompile "fastworkbench:FastWorkbench:${mc_version}:${fb_version}"

    // fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}:api")

   deobfCompile  "vazkii.patchouli:Patchouli:${patchouli_version}"
   deobfCompile "baubles:Baubles:1.12:${baubles_version}"
//  optional:
//   deobfCompile "compact-machines:compactmachines3-${mc_version}:${cm_version}:${cm_build}" 
}
repositories {
    maven { url "https://dvs1.progwml6.com/files/maven"    }
    maven { url "https://modmaven.k-4u.nl"  }
    maven { url "https://maven.theillusivec4.top/"    }
    maven { url "https://maven.blamejared.com" }
    maven { url "https://maven.blamejared.com/vazkii" }
    maven { url "http://minecraft.curseforge.com/api/maven/"    }
    maven { url 'https://www.cursemaven.com'  }
// ?? http://maven.thiakil.com/com/azanor/baubles/Baubles/
    // ?? https://files.minecraftforge.net/maven/
    //   maven {   url "https://tehnut.info/maven/"    } // 404s

}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

build.dependsOn signJar

task copyJar(type: Copy) {
    from jar
    into "${dist_folder}"
}

build.finalizedBy copyJar

task cleanJar {
    delete 'build/libs'
}
